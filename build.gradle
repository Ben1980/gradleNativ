buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

plugins {
  id "org.ysb33r.vfs" version "1.0"
}

apply plugin: 'cpp' 
apply plugin: 'google-test' 

apply from: 'gradle/setup-googletest.gradle'

model { 
    repositories {
        libs(PrebuiltLibraries) { 
            googleTest { 
                headers.srcDir new File(projectDir, "3rdparty/googletest-master/googletest/include") 
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file(projectDir.getPath() + "/3rdparty/googletest-master/googletest/build/" + findGoogleTestStaticLib(targetPlatform)) 
                }
            }
        }
    }
    components {
        greeter(NativeLibrarySpec) { }
        main(NativeExecutableSpec) { 
            sources {
                cpp.lib library: "greeter" 
            }
        }
    }


    binaries {
        withType(SharedLibraryBinarySpec) {
            if (toolChain in VisualCpp) {
                cppCompiler.define "DLL_EXPORT" 
            }
        }
        withType(GoogleTestTestSuiteBinarySpec) {
            lib library: "googleTest", linkage: "static" 
        
            if (targetPlatform.operatingSystem.linux) {
                cppCompiler.args '-pthread'
                linker.args '-pthread'
            }
        }
    }
}

def findGoogleTestStaticLib(Platform p) {
    if (p.operatingSystem.windows) {
        return "Release/gtest.lib"
    } else {
        return "libgtest.a"
    }
}